// Copyright (c) 2024-2025 Accenture, All Rights Reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//   http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// Description:
// This pipeline job deletes an existing GCP Cloud Workstation Configuration.
//
// References:
//

// Function to create a json object for the .tfvars.json file (on the fly)
def buildTfvarsMap(params, env) {
  return [
    sdv_cloud_ws_project_id: "${env.CLOUD_PROJECT}",
    sdv_cloud_ws_region: "${env.CLOUD_REGION}",
    sdv_cloud_ws_cluster_name: "${env.CLOUD_WS_CLUSTER_PRESET_NAME}",
    sdv_cloud_ws_input_config_name: "${params.CLOUD_WS_CONFIG_NAME}",
    sdv_cloud_ws_configs: [:]
  ]
}

pipeline {
  // Parameters defined in workloads/cloud-workstations/pipelines/config-admin-operations/delete-config/groovy/job.groovy

  agent {
    kubernetes {
      yaml """\
        apiVersion: v1
        kind: Pod
        spec:
          hostname: jenkins-cloud-ws-build-pod
          serviceAccountName: ${JENKINS_SERVICE_ACCOUNT}
          containers:
          - name: cloud-ws-builder
            image: ${CLOUD_REGION}-docker.pkg.dev/${CLOUD_PROJECT}/${CLOUD_WS_WORKLOADS_ENV_IMAGE_NAME}:latest
            imagePullPolicy: IfNotPresent
            command:
            - sleep
            args:
            - 5h
            tty: true
      """.stripIndent()
    }
  }

  environment {
    // Define the tf backend bucket name from jenkins env var
    def tfBackendBucket = "${CLOUD_BACKEND_BUCKET}"
    // Define the .tfvars.json file name where cloud-ws config tf directory is located
    def tfvarsJsonFilePath = "./workloads/cloud-workstations/terraform/config/config.tfvars.json"
    // this file is populated in stage `Delete Config Operation`
  }

  options {
    // Prevent any other "write" operation pipeline within the Cloud Workstations domain
    // from running concurrently with this job (and vice-versa).
    buildBlocker(useBuildBlocker: true, blockLevel: 'GLOBAL', scanQueueFor: 'BUILDABLE', blockingJobs: '(?i)Cloud-Workstations/.*/(create|delete|add|remove|start|stop).*')

    // Prevent this specific job from running concurrently with itself.
    disableConcurrentBuilds()
  }

  stages {
    stage('Delete Configuration Operation') {
      steps {
        script {
          echo 'Deleting Cloud WS Configuration...'
          // Prevent empty required params
          if(!params.CLOUD_WS_CONFIG_NAME?.trim()) {error("Required Parameter value NOT provided as input: CLOUD_WS_CONFIG_NAME. A Workstation Config name is needed.")}
          if(!params.CONFIRM_DELETE) {error("Required Parameter value NOT set to true: CONFIRM_DELETE must be selected in order to proceed for deletion.")}

          // Convert the tfvars map into valid json object and format it
          def tfvarsJson = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(buildTfvarsMap(params, env)))
          // Write the content to a temporary .tfvars.json file within the tf directory
          writeFile(file: "${tfvarsJsonFilePath}", text: tfvarsJson)

          def scriptPath = "./workloads/cloud-workstations/pipelines/config-admin-operations/delete-config/delete-config.sh"
          
          container(name: 'cloud-ws-builder') {
            catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
              sh """
                ${scriptPath} ${tfBackendBucket} ${tfvarsJsonFilePath}
              """
            }
          }
        }
      }
    }
  }

  // Post-build actions for success or failure
  post {
    success {
      echo "Status: SUCCESS - Workstation Configuration deletion job completed."
    }
    failure {
      echo "Status: FAILURE - Workstation Configuration deletion job failed."
    }
    always {
      // Clean-up
      echo "Job finished."
    }
  }
}
